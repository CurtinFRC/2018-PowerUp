apply plugin: "jaci.openrio.gradle.GradleRIO"
apply plugin: "cpp"

deploy {
    targets {
        target("roborio5333", jaci.openrio.gradle.frc.RoboRIO) {
            team = 5333
        }
    }
    artifacts {
        artifact("robotCode", jaci.openrio.gradle.frc.FRCNativeArtifact) {
            targets << "roborio5333"
            component = 'frcUserProgram'
            predeploy << { execute 'rm frcUserProgram' }
            debug = false
        }
        fileCollectionArtifact('paths') {
            files = project(':common').fileTree(dir: 'src/paths', include: '*.csv')
            directory = '/home/lvuser/paths'
            targets << "roborio5333"
        }
    }
}

wpi {
	ctreVersion = "5.2.2.0"
    wpilibVersion = "2018.4.1"
}

model {
    components {
        frcUserProgram(NativeExecutableSpec) {
            targetPlatform 'roborio'
            binaries.all {
              cppCompiler.args << '-g'
            }
            sources.cpp {
                source {
                    srcDir 'src/cpp'
                    include '**/*.cpp'
                }
                exportedHeaders {
                    srcDir 'src/include'
                    include '**/*.hpp', '**/*.h'
                }

                lib library: "wpilib"
                lib library: "ctre"
                lib library: "navx"
                lib library: "openrio.powerup.matchData"
                lib library: "pathfinder"
                lib project: ":common", library: "curtinFrcCommon", linkage: "static"
            }
        }
    }
}
